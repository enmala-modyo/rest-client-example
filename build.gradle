plugins {
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'jacoco'
    id 'org.sonarqube' version '3.5.0.2730'
    id 'org.cyclonedx.bom' version '1.7.2'
    id "name.remal.integration-tests" version "3.0.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "enmala_modyo-rest-client-example"
        property "sonar.organization", "enmala"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions", "**/**Application*,**/*Config*,**/*MapperImpl*"
        property "sonar.externalIssuesReportPaths", "trivy-report.json"
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenLocal()
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = 'https://maven.pkg.github.com/modyo/maven-packages'
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

group = 'com.modyo.example.client'
version = '1.0.0'

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply from: 'gradle/jacoco.gradle'

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.7.8'
    }
}

configurations {
    all {
        /* only junit 5 should be used */
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}
dependencies {

    // Spring Boot Framework
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // OpenFeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // Modyo Microservices Commons
    implementation 'com.modyo.ms.commons:core:2.10.2-RELEASE'
    implementation 'com.modyo.ms.commons:http:2.10.2-RELEASE'
    implementation 'com.modyo.ms.commons:aws-api-gw:2.10.2-RELEASE'

    // Swagger
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'io.swagger:swagger-annotations:1.6.9'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Mapstruts
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

    // Security Updates
    implementation 'org.yaml:snakeyaml:1.33'

    // Testing
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'com.tngtech.archunit:archunit:0.23.1'

    testImplementation 'org.mock-server:mockserver-junit-jupiter-no-dependencies:5.14.0'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.5"
    }
}

tasks.withType(Test).configureEach {
    systemProperty 'file.encoding', 'utf-8'
    useJUnitPlatform()
}

testSourceSets {
    integration
}

// slug size reduction for Heroku deployment
task stage {
    dependsOn build
    doLast {
        delete fileTree(dir: "build", exclude: "libs")
        delete fileTree(dir: "build/libs", exclude: "*.jar")
    }
}
